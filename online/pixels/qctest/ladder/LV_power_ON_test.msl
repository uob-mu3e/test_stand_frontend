# Author J. Guzman-Funck, March 2022, Cosmic Run. jose.guzman-funck19@imperial.ac.uk || pepe.guzmanfunck@gmail.com
#
#
# LV power ON test
# 
# Test description:
#
# 

# Turn on channel 2 HAMEG
# Loop over pixels. BIASBlock_On to zero all.
# BIASBlock_On to 5 each. Check current difference
# Output current difference on HAMEG channel 2


# Empty all buffers at startup
LOOP k, 3
    ODBSET /Equipment/PixelsCentral/QCTests/Ladder/LVPowerOn/Output/current_increase[$k-1], 0
ENDLOOP


PARAM half_ladder_id, "Half ladder ID"
PARAM hameg_id, "HAMEG ID"
PARAM channel, "HAMEG Channel"


#ODBGET /Equipment/PixelsCentral/QCTests/Ladder/half_ladder_id, _g_half_ladder_id
#IF $_g_half_ladder_id != half_ladder_id
#    MESSAGE "Input half_ladder_id at LV Power On Test different than the one inputed previously", 0
#ENDIF

#ODBGET /Equipment/PixelsCentral/QCTests/Ladder/half_ladder_id, _g_hameg_id
#IF $_g_hameg_id != hameg_id
#    MESSAGE "Input hameg_id at LV Power On Test different than the one inputed previously", 0
#ENDIF

#ODBGET /Equipment/PixelsCentral/QCTests/Ladder/half_ladder_id, _g_channel
#IF $_g_channel != channel
#    MESSAGE "Input channel at LV Power On Test different than the one inputed previously", 0
#ENDIF


ODBSET /Equipment/PixelsCentral/QCTests/Ladder/LVPowerOn/half_ladder_id, $half_ladder_id
ODBSET /Equipment/PixelsCentral/QCTests/Ladder/LVPowerOn/hameg_id, $hameg_id
ODBSET /Equipment/PixelsCentral/QCTests/Ladder/LVPowerOn/channel, $channel


CAT power_get_state_path, /Equipment/HAMEG, $hameg_id, /Variables/State[, $channel,]
CAT power_set_state_path, /Equipment/HAMEG, $hameg_id, /Variables/Set State[, $channel,]
CAT power_get_current_path, /Equipment/HAMEG, $hameg_id, /Variables/Current[, $channel,]


# Turn on channel $channel of HAMEG$hamge_id
ODBGET $power_get_state_path, now_state_hameg_channel

IF $now_state_hameg_channel == 0
    ODBSET $power_set_state_path, 1
    WAIT SECONDS, 5
ENDIF

SET _i, 0

# Turn off all
LOOP i, 3
    i_ = $i-1
    chip_id_ = $i_ + $half_ladder_id*3
    CAT path_to_bias, /Equipment/PixelsCentral/Settings/BIASDACS/, $chip_id_, /BiasBlock_On
    ODBSET $path_to_bias, 0
    ODBSET /Equipment/SwitchingCentral/Commands/MupixChipToConfigure, $chip_id_
    ODBSET /Equipment/SwitchingCentral/Commands/MupixConfig, 1
    WAIT SECONDS, 10
ENDLOOP

WAIT SECONDS, 3

LOOP j, 3
    # Save init current read
    ODBGET $power_get_current_path, init_current
    
    # Turn on chip i
    i_ = $j -1
    chip_id_ = $i_ + $half_ladder_id*3
    CAT path_to_bias, /Equipment/PixelsCentral/Settings/BIASDACS/, $chip_id_, /BiasBlock_On
    ODBSET $path_to_bias, 5
    ODBSET /Equipment/SwitchingCentral/Commands/MupixChipToConfigure, $chip_id_
    ODBSET /Equipment/SwitchingCentral/Commands/MupixConfig, 1

    # Read final current
    WAIT SECONDS, 10
    ODBGET $power_get_current_path, current
    WAIT SECONDS, 1

    # Turn off chip
    ODBSET $path_to_bias, 0
    ODBSET /Equipment/SwitchingCentral/Commands/MupixChipToConfigure, $chip_id_
    ODBSET /Equipment/SwitchingCentral/Commands/MupixConfig, 1

    WAIT SECONDS, 10

    # Save current difference
    diff = $current-$init_current
    ODBSET /Equipment/PixelsCentral/QCTests/Ladder/LVPowerOn/Output/current_increase[$i_], $diff
ENDLOOP
